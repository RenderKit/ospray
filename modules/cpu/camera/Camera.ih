// Copyright 2009-2021 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

#include "common/Ray.ih"
#include "rkcommon/math/box.ih"

/*! \file camera.ih Defines the abstract base class of an ISPC-side camera */

struct Camera;

/*! \brief Specifies the input parameters (time, screen, and lens
    samples) required for 'initRay' to generate a primary ray */
struct CameraSample
{
  vec2f screen; // normalized screen sample, in [0..1]
  vec2f lens;
  float time;
};

/*! \brief Fct pointer type for 'virtual' method that sets a pixel */
// generated ray.dir must be normalized to ensure ray.t is world-space distance
typedef void (*Camera_initRay)(const Camera *uniform,
    varying Ray &ray,
    const varying CameraSample &sample);

struct Camera
{
  Camera_initRay initRay; /*!< the 'virtual' initRay() method */

  bool motionBlur; // for the camera itself only, not in general

  float nearClip;
  box2f subImage; // viewable tile / subrange to compute, in [0..1]^2 x [0..1]^2
  range1f shutter; // camera shutter open start and end time, in [0..1]
  bool globalShutter;
  bool rollingShutterHorizontal;
  float rollingShutterDuration;
  RTCGeometry geom; // only to access rtcGetGeometryTransform
};

inline vec2f Camera_subRegion(const Camera *uniform self, const vec2f &screen)
{
  return lerp(screen, self->subImage.lower, self->subImage.upper);
}

inline float Camera_shutterTime(
    const Camera *uniform self, const vec2f &screen, float time)
{
  if (self->globalShutter)
    return lerp(time, self->shutter.lower, self->shutter.upper);

  time *= self->rollingShutterDuration;
  const float pos = self->rollingShutterHorizontal ? screen.x : screen.y;
  return clamp(time + lerp(pos, self->shutter.lower, self->shutter.upper));
}
