// Copyright 2009 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

#include "BSDF.ih"
#include "ShadingContext.ih"

OSPRAY_BEGIN_ISPC_NAMESPACE

inline BSDF_EvalRes Transmission_eval(const varying BSDF *uniform,
    const vec3f &,
    const vec3f &,
    const uniform FeatureFlagsHandler &)
{
  return make_BSDF_EvalRes_zero();
}

inline BSDF_SampleRes Transmission_sample(const varying BSDF *uniform self,
    const vec3f &wo,
    const vec2f &,
    float,
    const uniform FeatureFlagsHandler &)
{
  BSDF_SampleRes res;

  res.wi = neg(wo);
  res.pdf = inf;
  res.type = BSDF_SPECULAR_TRANSMISSION;
  res.weight = self->albedo;
  return res;
}

inline varying BSDF *uniform Transmission_create(
    uniform ShadingContext *uniform ctx,
    const varying linear3f *uniform frame,
    vec3f T)
{
  varying BSDF *uniform self =
      (varying BSDF * uniform) ShadingContext_alloc(ctx, sizeof(BSDF));
  BSDF_Constructor(self,
      T,
      BSDF_SPECULAR_TRANSMISSION,
      BSDF_TYPE_TRANSMISSION,
#ifdef OSPRAY_TARGET_SYCL
      NULL,
      NULL,
#else
      Transmission_eval,
      Transmission_sample,
#endif
      frame);
  return self;
}

OSPRAY_END_ISPC_NAMESPACE
