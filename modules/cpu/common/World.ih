// Copyright 2009-2021 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

// ospray stuff
#include "common/Clipping.ih"
#include "common/Ray.ih"
#include "common/VolumeIntervals.ih"

struct Renderer;
struct Light;

struct SciVisData
{
  // array containing the scene light sources
  // the lights are sorted: first lights that are not sampled (visible only),
  // then lights that are both sampled and visible
  Light **lights;
  uint32 numLights; // total number of light sources
  uint32 numLightsVisibleOnly; // number of lights that are not sampled (visible
                               // only)
  vec3f aoColorPi;
};

struct PathtracerData
{
  // array containing the scene light sources
  // the lights are sorted: first geometric, then virtual lights
  Light **lights;
  uint32 numLights; // total number of light sources (geometric + virtual)
  uint32 numGeoLights; // number of geometric light sources
  float *lightsCDF; // CDF used by NEE for randomly picking lights
};

struct World
{
  Instance **geometriesInst;
  int32 numGeometriesInst;

  Instance **volumesInst;
  int32 numVolumesInst;

  Instance **clippersInst;
  int32 numClippersInst;

  RTCScene embreeSceneHandleGeometries;
  RTCScene embreeSceneHandleVolumes;
  RTCScene embreeSceneHandleClippers;

  int32 numInvertedClippers;

  SciVisData scivisData;
  PathtracerData pathtracerData;
};

void World_Constructor(World *uniform world);

inline void traceGeometryRay(const World *uniform world, varying Ray &ray)
{
  // Skip if no geometries scene
  if (!world->embreeSceneHandleGeometries)
    return;

  uniform EmbreeIntersectionContext context;
  rtcInitIntersectContext(&context.ectx);
  rtcIntersectV(world->embreeSceneHandleGeometries,
      &context.ectx,
      (varying RTCRayHit * uniform) & ray);
}

inline void traceVolumeRay(
    const World *uniform world, varying Ray &ray, VolumeInterval &interval)
{
  initVolumeInterval(interval);
  if (!world->embreeSceneHandleVolumes)
    return;

  VolumeIntervals intervals;
  allocVolumeIntervals(intervals);

  uniform EmbreeIntersectionContext context;
  rtcInitIntersectContext(&context.ectx);
  context.userPtr = &intervals;
  rtcIntersectV(world->embreeSceneHandleVolumes,
      &context.ectx,
      (varying RTCRayHit * uniform) & ray);

  if (intervals.numVolumeIntervals > 0 && hasInterval(intervals.intervals[0])) {
    interval.instance = *(world->volumesInst + intervals.intervals[0].instID);
    interval.volumetricModel =
        interval.instance->group
            ->volumetricModels[intervals.intervals[0].geomID];
    interval.interval = intervals.intervals[0].interval;
  }
  freeVolumeIntervals(intervals);
}

inline void traceVolumeRay(
    const World *uniform world, varying Ray &ray, VolumeIntervals &intervals)
{
  if (!world->embreeSceneHandleVolumes)
    return;

  uniform EmbreeIntersectionContext context;
  rtcInitIntersectContext(&context.ectx);
  context.userPtr = &intervals;
  rtcIntersectV(world->embreeSceneHandleVolumes,
      &context.ectx,
      (varying RTCRayHit * uniform) & ray);

  if (intervals.numVolumeIntervals > 0) {
    for (uniform int i = 0; i < intervals.numVolumeIntervals; ++i) {
      const Instance *const instance =
          *(world->volumesInst + intervals.intervals[i].instID);
      intervals.intervals[i].instance = instance;
      intervals.intervals[i].volumetricModel =
          instance->group->volumetricModels[intervals.intervals[i].geomID];
    }
  }
}

// Intersection context structure used for clipping geometries
struct UserIntersectionContextForClipping
{
  uniform RTCIntersectContext ectx;
  const World *uniform world;
  varying int32 corrClippingDepth;
  varying uint32 hitsCount;
  varying ClippingHit hits[CLIPPING_HITS_MAX_COUNT];
};

unmasked void clippingIntersectionFilterV(
    const struct RTCFilterFunctionNArguments *uniform args);

inline void traceClippingRay(const World *uniform world,
    varying Ray &ray,
    varying RayIntervals &rayIntervals)
{
  // A scene with clipping geometries has to exist
  if (!world->embreeSceneHandleClippers) {
    rayIntervals.intervals[0] = make_box1f(ray.t0, ray.t);
    rayIntervals.count = 1;
    return;
  }

  // Create and initialize intersection context
  UserIntersectionContextForClipping context;
  rtcInitIntersectContext(&context.ectx);
  context.ectx.filter = clippingIntersectionFilterV;
  context.world = world;
  context.corrClippingDepth = 0;
  context.hitsCount = 0;

  // Intersect all geometry along given ray,
  // we have to temporarily extend the ray to inf because
  // even distant intersections affect visibilty of close objects
  float origT = ray.t;
  ray.t = inf;
  rtcIntersectV(world->embreeSceneHandleClippers,
      &context.ectx,
      (varying RTCRayHit * uniform) & ray);
  ray.t = origT;

  // Set initial clipping depth,
  // we have to apply correction because ray origin can be inside clipping area
  const uniform int32 voidClippingDepth = world->numInvertedClippers;
  int32 clippingDepth = voidClippingDepth - context.corrClippingDepth;

  // Variables used for intervals construction
  uint32 intervalId = 0;
  bool intervalCompleted = true;

  // Start ray interval if not in clipping area
  if (clippingDepth == 0) {
    rayIntervals.intervals[intervalId].lower = ray.t0;
    intervalCompleted = false;
  }

  // Iterate through collected hits and build ray intervals
  for (uint32 i = 0; i < context.hitsCount; i++) {
    // Do not build ray intervals that are further than ray.t
    float t = context.hits[i].t;
    float absT = abs(t);
    if (absT > ray.t)
      break;

    // Check if comming into or out of clipping area
    if (t < 0.0f) {
      clippingDepth--; // out of clipping area
    } else {
      clippingDepth++; // into clipping area
    }

    // Start interval if previous one is closed and we are not in clipping area
    if (and(clippingDepth == 0, intervalCompleted)) {
      rayIntervals.intervals[intervalId].lower = absT;
      intervalCompleted = false;
    }

    // End interval if current one is not closed and we enter clipping area
    if (and(clippingDepth > 0, !intervalCompleted)) {
      rayIntervals.intervals[intervalId].upper = absT;
      intervalCompleted = true;
      intervalId++;
    }
  }

  // Complete ray interval if started
  if (!intervalCompleted) {
    rayIntervals.intervals[intervalId].upper = ray.t;
    intervalId++;
  }

  // Save number of ray intervals
  rayIntervals.count = intervalId;
}

inline void traceGeometryRayIntervals(const World *uniform world,
    varying Ray &ray,
    varying RayIntervals &rayIntervals)
{
  // Save the ray
  const float t0 = ray.t0;
  const float t = ray.t;

  // Iterate through ray intervals
  for (uint32 i = 0; i < rayIntervals.count; i++) {
    // Set ray interval
    ray.t0 = rayIntervals.intervals[i].lower;
    ray.t = rayIntervals.intervals[i].upper;

    // Skip intervals outside of the ray range
    if (or (t < ray.t0, ray.t < t0))
      continue;

    // Clip interval to the ray range
    ray.t0 = max(ray.t0, t0);
    ray.t = min(ray.t, t);

    // Shoot the ray
    traceGeometryRay(world, ray);

    // Exit loop if geometry hit
    if (hadHit(ray)) {
      ray.t0 = t0;
      return;
    }
  }

  // Restore the ray
  ray.t0 = t0;
  ray.t = t;
}

inline void traceRay(const World *uniform world, varying Ray &ray)
{
  // Fast path if no clipping geometry
  if (!world->embreeSceneHandleClippers) {
    traceGeometryRay(world, ray);
    return;
  }

  // Trace ray in clipping geometries scene, fill array with ray intervals
  varying RayIntervals rayIntervals;
  traceClippingRay(world, ray, rayIntervals);

  // Trace ray intervals
  traceGeometryRayIntervals(world, ray, rayIntervals);
}

inline bool isOccludedNoClipping(const World *uniform world, varying Ray &ray)
{
  // Skip if no geometries scene
  if (!world->embreeSceneHandleGeometries)
    return false;

  uniform EmbreeIntersectionContext context;
  rtcInitIntersectContext(&context.ectx);
  rtcOccludedV(world->embreeSceneHandleGeometries,
      &context.ectx,
      (varying RTCRay * uniform) & ray);

  return ray.t < ray.t0;
}

inline bool areIntervalsOccluded(const World *uniform world,
    varying Ray &ray,
    varying RayIntervals &rayIntervals)
{
  // Iterate through ray intervals
  for (uint32 i = 0; i < rayIntervals.count; i++) {
    // Set ray interval
    ray.t0 = rayIntervals.intervals[i].lower;
    ray.t = rayIntervals.intervals[i].upper;

    // Check for occluders
    if (isOccludedNoClipping(world, ray))
      return true;
  }

  // No occluder found
  return false;
}

inline bool isOccluded(const World *uniform world, varying Ray &ray)
{
  // Fast path if no clipping geometry
  if (!world->embreeSceneHandleClippers) {
    return isOccludedNoClipping(world, ray);
  }

  // Allocate array for ray intervals
  varying RayIntervals rayIntervals;
  rayIntervals.count = 0;

  // Trace ray in clipping geometries scene, fill array with ray intervals
  traceClippingRay(world, ray, rayIntervals);

  // Is there any occluder within given ray intervals
  return areIntervalsOccluded(world, ray, rayIntervals);
}

/*! Perform post-intersect computations, i.e. fill the members of
    DifferentialGeometry. Should only get called for rays that actually hit
    that given world. Variables are calculated according to 'flags', a
    bit-combination of DG_PostIntersectFlags.
    The ray, dg.P, dg.Ng, and dg.Ns are in world-coordinates.
    Color defaults to vec4f(1.f) if queried but not present in geometry.
   */
inline void postIntersect(const World *uniform world,
    const Renderer *uniform renderer,
    varying DifferentialGeometry &dg,
    const varying Ray &ray,
    uniform int64 flags)
{
  dg.primID = ray.primID;
  dg.st = make_vec2f(ray.u, ray.v);

  dg.material = NULL;
  dg.renderer = renderer;

  if (flags & DG_COLOR)
    dg.color = make_vec4f(1.f);

  if (flags & DG_TANGENTS) {
    dg.dPds = make_vec3f(1.f, 0.f, 0.f);
    dg.dPdt = make_vec3f(0.f, 1.f, 0.f);
  }

  dg.P = ray.org + ray.t * ray.dir;
  dg.epsilon = 0.f; // per default no geometry-type specific epsilon

  foreach_unique (instID in ray.instID) {
    if (instID != RTC_INVALID_GEOMETRY_ID) {
      Instance *uniform instance = *(world->geometriesInst + instID);
      Instance_postIntersect(instance, renderer, dg, ray, flags);
    } else {
      dg.Ns = dg.Ng = ray.Ng;
    }
  }

  // merge geometry-type specific epsilon with general epsilon
  dg.epsilon = max(dg.epsilon, calcEpsilon(dg.P, ray.dir, ray.t));

// some useful combinations; enums unfortunately don't work :-(
#define DG_NG_FACEFORWARD (DG_NG | DG_FACEFORWARD)
#define DG_NS_FACEFORWARD (DG_NS | DG_FACEFORWARD)
#define DG_NG_NORMALIZE (DG_NG | DG_NORMALIZE)
#define DG_NS_NORMALIZE (DG_NS | DG_NORMALIZE)

  vec3f ffnng = normalize(dg.Ng);

  if ((flags & DG_NG_NORMALIZE) == DG_NG_NORMALIZE)
    dg.Ng = ffnng;

  if ((flags & DG_NS_NORMALIZE) == DG_NS_NORMALIZE)
    dg.Ns = normalize(dg.Ns);

  const bool flip = dot(ray.dir, dg.Ng) >= 0.f;
  if (flip)
    ffnng = neg(ffnng);

  if ((flags & DG_NG_FACEFORWARD) == DG_NG_FACEFORWARD && flip)
    dg.Ng = neg(dg.Ng);

  if ((flags & DG_NS_FACEFORWARD) == DG_NS_FACEFORWARD) {
    if (dot(dg.Ng, dg.Ns) < 0.f)
      dg.Ns = neg(dg.Ns);
  }

  dg.P = dg.P + dg.epsilon * ffnng;
#undef DG_NG_FACEFORWARD
#undef DG_NS_FACEFORWARD
#undef DG_NG_NORMALIZE
#undef DG_NS_NORMALIZE
}
