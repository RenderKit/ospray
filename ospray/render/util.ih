// Copyright 2009-2021 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

#include "common/World.ih"
#include "rkcommon/math/vec.ih"

inline vec3f make_random_color(const int i)
{
  const int mx = 13 * 17 * 43;
  const int my = 11 * 29;
  const int mz = 7 * 23 * 63;
  const uint32 g = (i * (3 * 5 * 127) + 12312314);
  return make_vec3f((g % mx) * (1.f / (mx - 1)),
      (g % my) * (1.f / (my - 1)),
      (g % mz) * (1.f / (mz - 1)));
}

float computeAO(const World *uniform,
    const varying DifferentialGeometry &,
    const uniform int sampleCnt,
    const uniform float aoRadius,
    const varying vec3i &sampleID);

/*! struct that stores a precomputed z-order for tiles of TILE_SIZE x TILE_SIZE
 * pixels */
struct z_order_t
{
  /*! 32-bit field specifying both x and y coordinate of the z-order,
      with upper 16 bits for the y coordinate, and lower 16 for the x
      coordinate. Compared to using two uint32-arrays, this saves on
      gather-loop */
  uniform uint32 xyIdx[TILE_SIZE * TILE_SIZE];
  uniform uint32 xs[TILE_SIZE * TILE_SIZE];
  uniform uint32 ys[TILE_SIZE * TILE_SIZE];
};

inline uint32 getZOrderX(const uint32 &xs16_ys16)
{
  return xs16_ys16 & (0xffff);
}
inline uint32 getZOrderY(const uint32 &xs16_ys16)
{
  return xs16_ys16 >> 16;
}

extern uniform z_order_t z_order;
extern uniform bool z_order_initialized;

// TODO should use PDEP
inline uniform uint32 partitionZOrder(uniform uint32 n)
{
  n &= 0x0000FFFF;
  n = (n | (n << 8)) & 0x00FF00FF;
  n = (n | (n << 4)) & 0x0F0F0F0F;
  n = (n | (n << 2)) & 0x33333333;
  n = (n | (n << 1)) & 0x55555555;
  return n;
}

// TODO should use PEXT
inline uniform uint32 unpartitionZOrder(uniform uint32 n)
{
  n &= 0x55555555;
  n = (n ^ (n >> 1)) & 0x33333333;
  n = (n ^ (n >> 2)) & 0x0F0F0F0F;
  n = (n ^ (n >> 4)) & 0x00FF00FF;
  n = (n ^ (n >> 8)) & 0x0000FFFF;
  return n;
}

inline uniform uint32 interleaveZOrder(uniform uint32 x, uniform uint32 y)
{
  return partitionZOrder(x) | (partitionZOrder(y) << 1);
}

inline void deinterleaveZOrder(
    uniform uint32 z, uniform uint32 *uniform x, uniform uint32 *uniform y)
{
  assert(x);
  assert(y);
  *x = *y = 0;
  *x = unpartitionZOrder(z);
  *y = unpartitionZOrder(z >> 1);
}

/*! precompute a per-pixel z-order to be used within a tile */
extern void precomputedZOrder_create();

/*! precompute a per-pixel z-order to be used within a tile */
inline void precomputeZOrder()
{
  if (!z_order_initialized)
    precomputedZOrder_create();
}

extern const z_order_t *uniform get_zorder();
