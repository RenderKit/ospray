// ======================================================================== //
// Copyright 2009-2016 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "BSDF.ih"
#include "GGXDistribution.ih"

struct MicrofacetDielectricLayer
{
  BSDF super;

  varying BSDF* uniform substrate;
  float eta;
  vec3f transmittance;
  float thickness;
  GGXDistribution distribution;
};

inline vec3f MicrofacetDielectricLayer_eval(const varying BSDF* uniform super,
                                            const vec3f& wo, const vec3f& wi, float& pdf)
{
  const varying MicrofacetDielectricLayer* uniform self = (const varying MicrofacetDielectricLayer* uniform)super;

  float cosThetaO = dot(wo, getN(super));
  float cosThetaI = dot(wi, getN(super));
  if (cosThetaO <= 0.0f || cosThetaI <= 0.0f)
  {
    pdf = 0.0f;
    return make_vec3f(0.0f);
  }

  // Compute the microfacet normal
  vec3f wh = normalize(wi + wo);
  float cosThetaOH = dot(wo, wh);

  // Fresnel term
  float F = fresnelDielectric(cosThetaOH, self->eta);

  // Evaluate the coating reflection
  float cosThetaH = dot(wh, getN(super));
  float cosThetaIH = dot(wi, wh);

  float whPdf;
  //float D = eval(self->distribution, cosThetaH, whPdf);
  float D = evalVisible(self->distribution, cosThetaH, cosThetaO, cosThetaOH, whPdf);
  float G = G2(self->distribution, cosThetaO, cosThetaI, cosThetaOH, cosThetaIH);

  float coatingPdf = whPdf * rcp(4.0f*cosThetaOH);
  float coatingValue = F * D * G * rcp(4.0f*cosThetaO);

  // Evaluate the substrate
  // Ignore refraction
  float substratePdf;
  vec3f substrateValue = self->substrate->eval(self->substrate, wo, wi, substratePdf);

  // Apply the coating transmittance
  // Computing the path length from the original angles would result in too much absorption, so instead use the refracted angles
  float cosThetaO1 = refract(cosThetaO, self->eta); // positive
  float cosThetaI1 = refract(cosThetaI, self->eta); // positive
  substrateValue = substrateValue * pow(self->transmittance, self->thickness * (rcp(cosThetaO1) + rcp(cosThetaI1)));

  // Compute the final substrate reflection
  // Ignore Fresnel for the exiting ray
  float Ft = 1.0f-fresnelDielectric(cosThetaO, self->eta);
  substrateValue = substrateValue * Ft;

  // Compute the total reflection
  float coatingPickProb = 1.0f-Ft;
  float substratePickProb = Ft;

  pdf = coatingPickProb * coatingPdf + substratePickProb * substratePdf;
  return coatingValue + substrateValue;
}

inline vec3f MicrofacetDielectricLayer_sample(const varying BSDF* uniform super,
                                              const vec3f& wo, vec3f& wi, float& pdf, BSDF_Type& type,
                                              const vec2f& s, float ss)
{
  const varying MicrofacetDielectricLayer* uniform self = (const varying MicrofacetDielectricLayer* uniform)super;

  float cosThetaO = dot(wo, getN(super));
  if (cosThetaO <= 0.0f) return make_vec3f(0.0f);

  // Sample the coating or the substrate reflection
  vec3f wh;
  float substratePdf;
  vec3f substrateValue;

  float Ft = 1.0f-fresnelDielectric(cosThetaO, self->eta);
  float coatingPickProb = 1.0f-Ft;
  float substratePickProb = Ft;

  if (ss < coatingPickProb)
  {
      // Sample the microfacet normal
      float whPdf;
      //wh = getFrame(super) * sample(self->distribution, whPdf, s);
      wh = getFrame(super) * sampleVisible(self->distribution, transposed(getFrame(super)) * wo, whPdf, s);
      float cosThetaOH = dot(wo, wh);

      type = BSDF_GLOSSY_REFLECTION;
      wi = reflect(wo, wh, cosThetaOH);

      // Evaluate the substrate
      substrateValue = self->substrate->eval(self->substrate, wo, wi, substratePdf);
  }
  else
  {
      // Sample the substrate
      float ss1 = (ss - coatingPickProb) * rcp(substratePickProb); // reallocate sample
      vec3f substrateWeight = self->substrate->sample(self->substrate, wo, wi, substratePdf, type, s, ss1);
      if (reduce_max(substrateWeight) <= 0.0f) return make_vec3f(0.0f);
      substrateValue = substrateWeight * substratePdf;

      // Compute the microfacet normal
      wh = normalize(wi + wo);
  }

  float cosThetaI = dot(wi, getN(super));
  if (cosThetaI <= 0.0f) return make_vec3f(0.0f);
  float cosThetaOH = dot(wo, wh);

  // Fresnel term
  float F = fresnelDielectric(cosThetaOH, self->eta);

  // Evaluate the coating reflection
  float cosThetaH = dot(wh, getN(super));
  float cosThetaIH = dot(wi, wh);

  float whPdf;
  //float D = eval(self->distribution, cosThetaH, whPdf);
  float D = evalVisible(self->distribution, cosThetaH, cosThetaO, cosThetaOH, whPdf);
  float G = G2(self->distribution, cosThetaO, cosThetaI, cosThetaOH, cosThetaIH);

  float coatingPdf = whPdf * rcp(4.0f*cosThetaOH);
  float coatingValue = F * D * G * rcp(4.0f*cosThetaO);

  // Apply the coating transmittance
  float cosThetaO1 = refract(cosThetaO, self->eta); // positive
  float cosThetaI1 = refract(cosThetaI, self->eta); // positive
  substrateValue = substrateValue * pow(self->transmittance, self->thickness * (rcp(cosThetaO1) + rcp(cosThetaI1)));

  // Compute the final base reflection
  substrateValue = substrateValue * Ft;

  // Compute the total reflection
  pdf = coatingPickProb * coatingPdf + substratePickProb * substratePdf;
  return (coatingValue + substrateValue) * rcp(pdf);
}

inline void MicrofacetDielectricLayer_Constructor(varying MicrofacetDielectricLayer* uniform self, const varying linear3f* uniform frame,
                                                  varying BSDF* uniform substrate, float eta, vec3f transmittance, float thickness, float roughness)
{
  BSDF_Constructor(&self->super, BSDF_GLOSSY_REFLECTION | substrate->type,
                   MicrofacetDielectricLayer_eval, MicrofacetDielectricLayer_sample,
                   frame);

  self->substrate = substrate;
  self->eta = eta;
  self->transmittance = transmittance;
  self->thickness = thickness;
  self->distribution = make_GGXDistribution(roughnessToAlpha(roughness));
}

inline varying BSDF* uniform MicrofacetDielectricLayer_create(uniform ShadingContext* uniform ctx, const varying linear3f* uniform frame,
                                                              varying BSDF* uniform substrate, float eta, vec3f transmittance, float thickness, float roughness)
{
  varying MicrofacetDielectricLayer* uniform self = (varying MicrofacetDielectricLayer* uniform)ShadingContext_alloc(ctx, sizeof(MicrofacetDielectricLayer));
  MicrofacetDielectricLayer_Constructor(self, frame, substrate, eta, transmittance, thickness, roughness);
  return &self->super;
}
