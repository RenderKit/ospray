// ======================================================================== //
// Copyright 2009-2017 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use self file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "BSDF.ih"
#include "GGXDistribution.ih"

struct MicrofacetConductor
{
  BSDF super;

  vec3f reflectance;
  vec3f eta;
  vec3f k;
  GGXDistribution distribution;
};

inline BSDF_EvalRes MicrofacetConductor_eval(const varying BSDF* uniform super,
                                             const vec3f& wo, const vec3f& wi)
{
  const varying MicrofacetConductor* uniform self = (const varying MicrofacetConductor* uniform)super;
  BSDF_EvalRes res;

  float cosThetaO = dot(wo, getN(super));
  float cosThetaI = dot(wi, getN(super));
  if (cosThetaO <= 0.0f || cosThetaI <= 0.0f) return make_BSDF_EvalRes_zero();

  vec3f wh = normalize(wi + wo);
  float cosThetaH = dot(wh, getN(super));
  float cosThetaOH = dot(wo, wh);
  float cosThetaIH = dot(wi, wh);

  vec3f F = fresnelConductor(cosThetaOH, self->eta, self->k);
  float whPdf;
  //float D = eval(self->distribution, cosThetaH, whPdf);
  float D = evalVisible(self->distribution, cosThetaH, cosThetaO, cosThetaOH, whPdf);
  float G = G2(self->distribution, cosThetaO, cosThetaI, cosThetaOH, cosThetaIH);

  res.pdf = whPdf * rcp(4.0f*abs(cosThetaOH));
  res.value = F * (D * G * rcp(4.0f*cosThetaO));
  return res;
}

inline BSDF_SampleRes MicrofacetConductor_sample(const varying BSDF* uniform super,
                                                 const vec3f& wo, const vec2f& s, float ss)
{
  const varying MicrofacetConductor* uniform self = (const varying MicrofacetConductor* uniform)super;
  BSDF_SampleRes res;

  float cosThetaO = dot(wo, getN(super));
  if (cosThetaO <= 0.0f) return make_BSDF_SampleRes_zero();

  float whPdf;
  //vec3f wh = getFrame(super) * sample(self->distribution, whPdf, s);
  vec3f wh = getFrame(super) * sampleVisible(self->distribution, transposed(getFrame(super)) * wo, whPdf, s);

  res.wi = reflect(wo, wh);
  float cosThetaI = dot(res.wi, getN(super));
  if (cosThetaI <= 0.0f) return make_BSDF_SampleRes_zero();
  //float cosThetaH = dot(wh, getN(super));
  float cosThetaOH = dot(wo, wh);
  float cosThetaIH = dot(res.wi, wh);

  vec3f F = fresnelConductor(cosThetaOH, self->eta, self->k);
  float G = G2(self->distribution, cosThetaO, cosThetaI, cosThetaOH, cosThetaIH);

  res.type = BSDF_GLOSSY_REFLECTION;
  res.pdf = whPdf * rcp(4.0f*abs(cosThetaOH));
  //res.weight = F * (G * abs(cosThetaOH * rcp(cosThetaO*cosThetaH)));
  res.weight = F * (G * rcp_safe(G1(self->distribution, cosThetaO, cosThetaOH)));
  return res;
}

inline void MicrofacetConductor_Constructor(varying MicrofacetConductor* uniform self, const varying linear3f* uniform frame,
                                            vec3f reflectance, vec3f eta, vec3f k, float roughness)
{
  BSDF_Constructor(&self->super, BSDF_GLOSSY_REFLECTION,
                   MicrofacetConductor_eval, MicrofacetConductor_sample,
                   frame);

  self->reflectance = reflectance;
  self->eta = eta;
  self->k = k;
  self->distribution = make_GGXDistribution(roughnessToAlpha(roughness));
}

inline varying BSDF* uniform MicrofacetConductor_create(uniform ShadingContext* uniform ctx, const varying linear3f* uniform frame,
                                                        vec3f reflectance, vec3f eta, vec3f k, float roughness)
{
  varying MicrofacetConductor* uniform self = (varying MicrofacetConductor* uniform)ShadingContext_alloc(ctx, sizeof(MicrofacetConductor));
  MicrofacetConductor_Constructor(self, frame, reflectance, eta, k, roughness);
  return &self->super;
}
