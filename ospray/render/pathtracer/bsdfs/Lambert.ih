// ======================================================================== //
// Copyright 2009-2015 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "BSDF.ih"

struct Lambert
{
  BSDF super;
  vec3f R;
};

inline vec3f Lambert_eval(const varying BSDF* uniform super,
                          const DifferentialGeometry& dg, const vec3f& wo, const vec3f& wi, float& pdf)
{
  const varying Lambert* uniform self = (const varying Lambert* uniform)super;
  pdf = clamp(dot(wi, getN(super))) * one_over_pi;
  return self->R/pi * clamp(dot(wi, getN(super)));
}

inline vec3f Lambert_sample(const varying BSDF* uniform super,
                            const DifferentialGeometry& dg, const vec3f& wo, vec3f& wi, float& pdf, BSDFType& type,
                            const vec2f& s, float ss)
{
  const varying Lambert* uniform self = (const varying Lambert* uniform)super;
  const vec3f localDir = cosineSampleHemisphere(s);
  wi = getFrame(super) * localDir;
  pdf = cosineSampleHemispherePDF(localDir);
  type = BSDF_DIFFUSE_REFLECTION;
  return self->R;
}

inline void Lambert_Constructor(varying Lambert* uniform self, const varying linear3f* uniform frame,
                                const vec3f& R)
{
  BSDF_Constructor(&self->super, BSDF_DIFFUSE_REFLECTION,
                   Lambert_eval, Lambert_sample,
                   frame);
  self->R = R;
}

inline varying BSDF* uniform Lambert_create(uniform ShadingContext* uniform ctx, const varying linear3f* uniform frame,
                                            const vec3f& R)
{
  varying Lambert* uniform self = (varying Lambert* uniform)ShadingContext_alloc(ctx, sizeof(Lambert));
  Lambert_Constructor(self, frame, R);
  return &self->super;
}
