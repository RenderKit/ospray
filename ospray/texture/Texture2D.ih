// ======================================================================== //
// Copyright 2009-2017 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "ospray/OSPTexture.h"
#include "math/vec.ih"

struct Texture2D;

typedef varying vec4f (*Texture2D_get)(const uniform Texture2D *uniform self,
                                       const varying vec2f &p);

typedef varying vec3f (*Texture2D_getN)(const uniform Texture2D *uniform self,
                                        const varying vec2f &p);

struct Texture2D {
  vec2i         size;
  vec2f         sizef;     // size, as floats; slightly smaller than 'size' to avoid range checks
  vec2f         halfTexel; // 0.5/size, needed for bilinear filtering and clamp-to-edge
  Texture2D_get get;
  Texture2D_getN getNormal;
  void         *data;
  bool          hasAlpha; // 4 channel texture?
};

// XXX won't work with MIPmapping: clean implementation with clamping on integer coords needed then 
inline vec2f clamp2edge(const uniform Texture2D *uniform self, const vec2f p)
{
  return clamp(p, self->halfTexel, 1.0f - self->halfTexel);
}

inline uniform bool hasAlpha(const uniform Texture2D *uniform self)
{
  return (self == NULL) ? false : self->hasAlpha;
}

/*! helper function that returns the sampled value for the first
  channel of the given texture

  Right now, this function always asks the texture for all four
  channels, and then discards all but one; later implementations may
  have specialized 'get1f' methods with the texture

  \note self may NOT be NULL!
*/
inline float get1f(const uniform Texture2D *uniform self,
                   const varying vec2f where)
{
  vec4f ret = self->get(self, where);
  return ret.x;
}

/*! helper function that returns the sampled value for the first three
  channels of the given texture

  Right now, this function always asks the texture for all four
  channels, and then discards all but one; later implementations may
  have specialized 'get3f' methods with the texture

  \note self may NOT be NULL!
*/
inline vec3f get3f(const uniform Texture2D *uniform self,
                   const varying vec2f where)
{
  vec4f ret = self->get(self, where);
  return make_vec3f(ret);
}

/*! helper function that returns the sampled value of the four
  channels of the given texture.

  Note that it's up to the texture to define clearly what happens if
  we ask for four channels even if the texture has less physical
  channels.

  \note self may NOT be NULL!
*/
inline vec4f get4f(const uniform Texture2D *uniform self,
                   const varying vec2f where)
{
  return self->get(self, where);
}

/*! helper function that returns the sampled values interpreted as a normal */
inline vec3f getNormal(const uniform Texture2D *uniform self,
                       const varying vec2f where)
{
  if (self == NULL) return make_vec3f(0.f, 0.f, 1.f);
  else return self->getNormal(self, where);
}


/*! helper function: get1f() with a default value if the texture is NULL */
inline float get1f(const uniform Texture2D *uniform self,
                   const varying vec2f where,
                   const varying float defaultValue)
{
  if (self == NULL) return defaultValue;
  else return get1f(self,where);
}

/*! helper function: get3f() with a default value if the texture is NULL */
inline vec3f get3f(const uniform Texture2D *uniform self,
                   const varying vec2f where,
                   const varying vec3f defaultValue)
{
  if (self == NULL) return defaultValue;
  else return get3f(self,where);
}

/*! helper function: get4f() with a default value if the texture is NULL */
inline vec4f get4f(const uniform Texture2D *uniform self,
                   const varying vec2f where,
                   const varying vec4f defaultValue)
{
  if (self == NULL) return defaultValue;
  else return get4f(self,where);
}
