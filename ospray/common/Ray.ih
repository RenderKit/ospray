// Copyright 2009-2019 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

// ospray
#include "../math/AffineSpace.ih"
#include "../math/box.ih"
#include "../math/vec.ih"
// embree
#include "embree3/rtcore.isph"

struct VolumetricModel;

// NOTE(jda) - this MUST match Embree's RayHit structure layout!
struct Ray
{
  /* ray input data */
  vec3f org; /*!< ray origin */
  float t0; /*!< start of valid ray interval */

  vec3f dir; /*!< ray direction */
  float time; //!< Time of this ray for motion blur

  float t; /*!< end of valid ray interval, or distance to hit point after
              'intersect' */
  int32 mask; //!< Used to mask out objects during traversal
  int32 rayID;
  int32 flags;

  /* hit data */
  vec3f Ng; /*! geometry normal*/

  float u; //!< Barycentric u coordinate of hit
  float v; //!< Barycentric v coordinate of hit

  int primID; //!< primitive ID
  int geomID; //!< geometry ID
  int instID; //!< instance ID
};

struct EmbreeIntersectionContext
{
  RTCIntersectContext ectx;
  void *uniform userPtr;
};

// Hit query functions ////////////////////////////////////////////////////////

inline bool noHit(const Ray &ray)
{
  return ray.geomID == RTC_INVALID_GEOMETRY_ID;
}

inline bool hadHit(const Ray &ray)
{
  return !noHit(ray);
}

// Ray initialization /////////////////////////////////////////////////////////

inline void setRay(Ray &ray,
    const vec3f &ray_org,
    const vec3f &ray_dir,
    const float t0,
    const float t1,
    const float time = 0.f)
{
  ray.org = ray_org;
  ray.dir = ray_dir;
  ray.t0 = t0;
  ray.t = t1;
  ray.time = time;
  ray.mask = -1;
  ray.geomID = -1;
  ray.primID = -1;
  ray.instID = -1;
}

inline void setRay(Ray &ray,
    const vec3f &ray_org,
    const vec3f &ray_dir,
    const float time = 0.f)
{
  setRay(ray, ray_org, ray_dir, 0.f, inf, time);
}

inline void setRay(Ray &ray, const float t0, const float t1)
{
  setRay(ray, ray.org, ray.dir, t0, t1, ray.time);
}

// Ray transformation helpers /////////////////////////////////////////////////

inline void transformRay(Ray &ray, AffineSpace3f &xfm)
{
  ray.org = xfmPoint(xfm, ray.org);
  ray.dir = xfmVector(xfm, ray.dir);
  ray.Ng = xfmVector(transposed(xfm.l), ray.Ng);
}

inline void transformRay(Ray &ray, AffineSpace3f xfm)
{
  ray.org = xfmPoint(xfm, ray.org);
  ray.dir = xfmVector(xfm, ray.dir);
  ray.Ng = xfmVector(transposed(xfm.l), ray.Ng);
}

// Volume hit structure ///////////////////////////////////////////////////////

struct VolumeInterval
{
  VolumetricModel *volumetricModel;
  range1f interval;
  AffineSpace3f xfm;
  AffineSpace3f rcp_xfm;
  unsigned int primID;
  unsigned int geomID;
  unsigned int instID;
};

inline void initVolumeInterval(VolumeInterval &hit)
{
  hit.volumetricModel = NULL;
  hit.interval.lower = inf;
  hit.interval.upper = -inf;
}

inline bool hasInterval(const VolumeInterval &vi)
{
  return !isEmpty(vi.interval);
}

#define NUM_VOLUME_INTERVALS_MAX 10
struct VolumeIntervals
{
  unsigned int numVolumeIntervals;
  VolumeInterval intervals[NUM_VOLUME_INTERVALS_MAX];
};

inline void initVolumeIntervals(VolumeIntervals &intervals)
{
  intervals.numVolumeIntervals = 0;
  initVolumeInterval(intervals.intervals[0]);
}
